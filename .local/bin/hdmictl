#!/usr/bin/env fish

# Prompt user to change the monitor display layout
# Dependencies: xrandr bspc hsetroot

# TODO: dynamic based on connected displays?
set MON0 "eDP-1-1"
set MON1 "HDMI-0"

test $argv[1]
and set choice $argv[1]
or set choice (choose "docked" "mirrored" "disabled")
test $choice
or return 1

while test "$choice" != "disabled" && not bspc query --monitors $MON1
	test "$(choose "retry" "cancel")" != "retry"
	and return
end

hsetroot -solid "#000000"

switch $choice
	case "docked"
		xrandr --output $MON0 --auto --primary --output $MON1 --auto --rotate right --left-of $MON0
		bspc monitor $MON0 --reset-desktops 1 2 3 4 5 6 7 8 9
		bspc monitor $MON1 --reset-desktops 0
	case "mirrored"
		xrandr --output $MON0 --auto --primary --output $MON1 --auto --rotate normal --same-as $MON0
		bspc monitor $MON0 --reset-desktops 1 2 3 4 5 6 7 8 9 0
		bspc monitor $MON1 --remove
	case "disabled"
		xrandr --output $MON0 --auto --primary --output $MON1 --off
		bspc monitor $MON0 --reset-desktops 1 2 3 4 5 6 7 8 9 0
		bspc monitor $MON1 --remove
end

bspc wm --adopt-orphans
hsetroot -solid "#0e0e0e"
