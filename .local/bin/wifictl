#!/usr/bin/env fish

# Allow user to connect to Wifi networks
# Dependencies: NetworkManager

test $argv[1]
and set choice $argv[1]
or set choice (choose "<rescan networks>" (TERM="" nmcli --fields bars,ssid,bssid,security,in-use --mode tabular device wifi list --rescan no | sed -n 1!p))

switch $choice
	case ""
		return
	case "<rescan networks>"
		nmcli device wifi rescan
		sleep 0.25
		eval (status filename)
	case "*"
		set id (random)
		set ssid (string match --regex "(?<=  ).*?(?=  )" $choice)
		set bssid (string match --regex "..:..:..:..:..:.." $choice)
		if string match --regex "\\*\\W*\$" $choice
			nmcli connection down id $ssid
			notify-send -i network-wireless-disconnected -r $id "Disconnected from wifi network" $ssid
		else
			notify-send -i network-wireless-acquiring -r $id -t 0 "Connecting to wifi network..." $ssid
			if nmcli connection up id $ssid || nmcli device wifi connect $bssid password (choose -p "Password for $ssid:" -c -l 0) && test -n "$(nmcli --fields ap.signal device show | head -1)"
				set strength (nmcli --terse --fields ap.signal --mode tabular device show | head -1)
				set icon network-wireless-connected-(math "round($strength / 25) * 25")
				nm-online --quiet --timeout 5
				and notify-send -i $icon -r $id "Connected - Internet access available" $ssid
				or notify-send -i $icon -r $id "Connected - No internet access" $ssid
			else
				notify-send -i network-wireless-disconnected -r $id "Unable to connect to $ssid" "Wrong password?"
			end
		end
end
