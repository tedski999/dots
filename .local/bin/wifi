#!/usr/bin/env fish

# Allow user to connect to Wifi networks
# Dependencies: NetworkManager

set id (random)

set list (nmcli --fields bars,ssid,bssid,in-use --mode tabular device wifi list --rescan no | sed -n 1!p)
set choice (choose -p " Wifi" -w 500 $list)
test $choice
or return 1

set ssid (string match --regex "(?<=  ).*?(?=  )" $choice)
set bssid (string match --regex "..:..:..:..:..:.." $choice)

# TODO: (attempt) autoconnect if already known
set pass (input -p " Password")
test $pass
and set pass "password $pass"

notify-send -i network-wireless-acquiring -r $id -t 0 "Connecting to wifi network..." $ssid
if nmcli device wifi connect $bssid $pass
	set strength (nmcli --terse --fields ap.signal --mode tabular device show | head -1)
	set icon network-wireless-connected-(math "round($strength / 25) * 25")
	nm-online --quiet --timeout 5
	and notify-send -i $icon -r $id "Connected - Internet access available" $ssid
	or notify-send -i $icon -r $id "Connected - No internet access" $ssid
else
	notify-send -i network-wireless-disconnected -r $id "Unable to connect to $ssid" "Wrong password?"
end
