#!/usr/bin/env fish

# Protect user from an empty battery by sending notifications and entering hibernation
# Dependencies: acpi

set id (random)
set old_charge 100

while true
	acpi | sed "s/.*: //" | read --delimiter ", " state charge time

	if test $state = "Discharging"
		set state (string lower $state)
		test $state = "discharging"
		and set state "unplugged"
		set charge (string trim -rc % $charge)
		set time "About $time"

		if test $old_charge -gt 2 -a $charge -le 2
			for i in (seq 5 -1 1)
				notify-send -i "battery-020" -u "critical" -r $id -t 0 "Battery empty!" "Hibernating in $i..."
				sleep 1
			end
			systemctl hibernate
		else if test $old_charge -gt 10 -a $charge -le 10
			notify-send -i "battery-020" -u "critical" -r $id -t 0 "Battery critical!" "$time"
		else if test $old_charge -gt 20 -a $charge -le 20
			notify-send -i "battery-020" -u "normal" -r $id "Battery low!" "$time"
		end

		set old_charge $charge
	end

	sleep 1
end
